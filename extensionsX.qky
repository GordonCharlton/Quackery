
  [ this ] is extensions.qky

  [ $ 'bigrat.qky' loadfile ] now!

  [ stack ]                     is merge.test  (         --> s       )
  [ stack ]                     is temp.test   (         --> s       )

  [ ]'[ merge.test put
    [] temp.test put
    [ dup  [] != while
      over [] != while
      over 0 peek
      over 0 peek
      merge.test share do
      not if dip
        [ 1 split
          temp.test take
          rot join
          temp.test put ]
        again ]
      join
      temp.test take swap join
      merge.test release  ]     is mergewith   (     [ [ --> [       )

  [ stack ]                     is sort.test   (         --> s       )

  [ ]'[ sort.test put
    [ dup size 16 < iff
        [ sortwith [ sort.test share do ] ]
        done
      dup size 2 / split
      swap recurse
      swap recurse
      mergewith [ sort.test share do ] ]
    sort.test release ]         is sortwith    (       [ --> [       )

  [ sortwith > ]                is sort        (       [ --> [       )

  [ sortwith $> ]               is sort$       (       [ --> [       )

  [ not iff 
      ]else[ done
    ]'[ do ]done[ ]             is cond        (       b -->         )

